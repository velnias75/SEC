import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

apply plugin: 'java'

version = '1.2.1'

repositories {
    maven { url = uri("https://repo.papermc.io/repository/maven-public/") }
}

dependencies {

    compileOnly("org.spigotmc:spigot-api:${project.spigot_version}")
    
    implementation "org.bstats:bstats-bukkit:${project.bstats_version}"
	shadow "org.bstats:bstats-bukkit:${project.bstats_version}"
	
	implementation(project(path: ':spiget', configuration: 'shadow')) {
		exclude group:'dev.derklaro.spiget'
	}
	
	shadow(project(':spiget')) 
}

task submodulesUpdate(type:Exec) {
  description 'Updates (and inits) git submodules'
  commandLine 'git', 'submodule', 'update', '--init', '--recursive', '--remote'
  group 'Build Setup'
}

compileJava {
	dependsOn submodulesUpdate
}

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(16))
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
	duplicatesStrategy = 'include'
        filter ReplaceTokens, tokens: [version: version]
    }
}

shadowJar {

	archiveClassifier.set('')
    
    relocate 'org.bstats', 'de.rangun.sec.shadowed.org.bstats'
    relocate 'de.rangun.spiget', 'de.rangun.sec.shadowed.de.rangun.spiget'

    exclude 'META-INF/**'
    
    minimize()
}

assemble {
	dependsOn shadowJar
}
